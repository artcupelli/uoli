# Uoli - Arduino

# üëã Introdu√ß√£o

Esse diret√≥rio apresenta a montagem do projeto em arduino

O app foi desenvolvido com linguagem **C** utilizando de algumas bibliotecas dispon√≠veis na internet.

---

# üöó Componentes Principais
O carrinho √© composto por 4 componentes principais:

1. Driver Motor de ponte H (Controlador de motores/rodas)
2. ESP32-CAM (C√¢mera Wireless)
3. Display LCD TFT 2.4 touchscreen shield (Tela/Rosto rob√¥)
4. Arduino Mega

## Controlador de motor
 
 Este componente √© chamado de Driver Motor Ponte H e serve para controlar os motores do carrinho.
 Esse m√≥dulo trabalho basicamente com 4 portas de sa√≠da e 4 portas de entrada. Cada dupla de portas de entrada √© respos√°vel por controlar um dos motores, onde colocar um dos pinos em high e o outro em low resultar√° em um movimento para traz ou para frente.
 
  ![image]( https://i2.wp.com/randomnerdtutorials.com/wp-content/uploads/2018/05/L298N-label.jpg?w=750&quality=100&strip=all&ssl=1)
  
Em nosso projeto, estamos apenas fazendo o carro ir para frente, tr√°s, direita e esquerda, sem controle de velocidade. Por isso, as portas ENABLE A e B n√£o est√£o sendo utilizadas. A energia √© conectada na entrada +5v e GND e os motores s√£o conectados nas portas OUT1 e OUT2 (Motor direita) e OUT3 e OUT4 (Motor esquerda). 
**Quanto a programa√ß√£o do m√≥dulo, ela √© feita juntamente com o arduino mega

## ESP32-CAM
A ESP32-CAM √© um tipo de arduino que j√° vem equipado com conex√£o sem fio Wi-Fi e uma c√¢mera, capaz de tirar fotos, gravar v√≠deos e, o objeto de interesse para o projeto, fazer trasmiss√£o em rede das imagens captadas pela c√¢mera.

  ![image](https://images.tcdn.com.br/img/img_prod/650361/esp32_cam_wifi_bluetooth_com_camera_inclusa_2465_11_20200205161931.jpg)

Em nosso projeto, a c√¢mera √© respons√°vel por:
1. Gerar uma rede wi-fi para conex√£o do usu√°rio
2. Captar e transmitir a imagem na rede criada
3. Receber e enviar as requisi√ß√µes de rede dadas pelo usu√°rio para o Arduino Mega, via conex√£o Serial.

O autor c√≥digo consultado para criar o programa carregada na ESP32-CAM foi o Rui Santos e o site com o c√≥digo original pode ser encontrada no link abaixo.
https://randomnerdtutorials.com/esp32-cam-car-robot-web-server/

O c√≥digo carregado na ESP32-CAM tamb√©m pode ser visto abaixo, em seguida, explicaremos as principais partes do c√≥digo.
```c
/*********
  Rui Santos
  Complete instructions at https://RandomNerdTutorials.com/esp32-cam-projects-ebook/
  
  Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files.
  The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.
*********/

#include "esp_camera.h"
#include <WiFi.h>
#include "esp_timer.h"
#include "img_converters.h"
#include "Arduino.h"
#include "fb_gfx.h"
#include "soc/soc.h"             // disable brownout problems
#include "soc/rtc_cntl_reg.h"    // disable brownout problems
#include "esp_http_server.h"

#define PART_BOUNDARY "123456789000000000000987654321"

#define CAMERA_MODEL_AI_THINKER
#define PWDN_GPIO_NUM     32
#define RESET_GPIO_NUM    -1
#define XCLK_GPIO_NUM      0
#define SIOD_GPIO_NUM     26
#define SIOC_GPIO_NUM     27

#define Y9_GPIO_NUM       35
#define Y8_GPIO_NUM       34
#define Y7_GPIO_NUM       39
#define Y6_GPIO_NUM       36
#define Y5_GPIO_NUM       21
#define Y4_GPIO_NUM       19
#define Y3_GPIO_NUM       18
#define Y2_GPIO_NUM        5
#define VSYNC_GPIO_NUM    25
#define HREF_GPIO_NUM     23
#define PCLK_GPIO_NUM     22

// Replace with your network credentials
const char* ssid = "UoLi";
const char* password = "Carrinho";

static const char* _STREAM_CONTENT_TYPE = "multipart/x-mixed-replace;boundary=" PART_BOUNDARY;
static const char* _STREAM_BOUNDARY = "\r\n--" PART_BOUNDARY "\r\n";
static const char* _STREAM_PART = "Content-Type: image/jpeg\r\nContent-Length: %u\r\n\r\n";

httpd_handle_t camera_httpd = NULL;
httpd_handle_t stream_httpd = NULL;
static const char PROGMEM INDEX_HTML[] = R"rawliteral(
<html>
  <head>
    <title>ESP32-CAM Robot</title>
    <meta name="viewport" content="width=device-width, initial-scale=1">
    <style>
      body { font-family: Arial; text-align: center; margin:0px auto; background-color: #1b1b1b;}
      table { margin-left: auto; margin-right: auto; }
      td { padding: 8 px; }
      .button {
        background-color: #8969fb;
        border: none;
        color: white;
        padding: 10px 20px;
        text-align: center;
        text-decoration: none;
        display: inline-block;
        font-size: 18px;
        margin: 6px 3px;
        cursor: pointer;
        width: 100%;
        -webkit-touch-callout: none;
        -webkit-user-select: none;
        -khtml-user-select: none;
        -moz-user-select: none;
        -ms-user-select: none;
        user-select: none;
        -webkit-tap-highlight-color: rgba(0,0,0,0);
        
      }
      .button::placeholder{
          color: white;
      }
      img {  
        width: 100% ;
        height: auto ; 
        border: 1px solid #8969fb;
        border-radius: 10px;
        margin-bottom: 100px;
        margin-right: 50px;
      }
    </style>
  </head>
  <body>
    <img src="" id="photo" >
    <table>
      <tr><td colspan="3" align="center"><button class="button" ontouchstart="toggleCheckbox('forward');" ontouchend="toggleCheckbox('stop');" >Forward</button></td></tr>
      <tr><td align="center"><button class="button" ontouchstart="toggleCheckbox('left');" ontouchend="toggleCheckbox('stop');">Left</button></td><td align="center"><button class="button" ontouchstart="toggleCheckbox('stop');">Stop</button></td><td align="center"><button class="button" ontouchstart="toggleCheckbox('right');"  ontouchend="toggleCheckbox('stop');">Right</button></td></tr>
      <tr><td colspan="3" align="center"><button class="button" ontouchstart="toggleCheckbox('backward');" ontouchend="toggleCheckbox('stop');">Backward</button></td></tr>                   
    <tr><td colspan="2" align="center"><button class="button" ontouchstart="toggleCheckbox('l');">Light</button></td><td colspan="1" align="center"><button class="button" ontouchstart="toggleCheckbox('M'+document.getElementById('texto').value);">Send</button></td></tr>
    <tr><td colspan="3" align="center"><input id="texto" class="button" placeholder="Mensagem"></td></tr>
    </table>
    
   <script>
   function toggleCheckbox(x) {
     var xhr = new XMLHttpRequest();
     xhr.open("GET", "/action?go=" + x, true);
     xhr.send();
   }
   window.onload = document.getElementById("photo").src = window.location.href.slice(0, -1) + ":81/stream";
  </script>
  </body>
</html>
)rawliteral";

static esp_err_t index_handler(httpd_req_t *req){
  httpd_resp_set_type(req, "text/html");
  return httpd_resp_send(req, (const char *)INDEX_HTML, strlen(INDEX_HTML));
}

static esp_err_t stream_handler(httpd_req_t *req){
  camera_fb_t * fb = NULL;
  esp_err_t res = ESP_OK;
  size_t _jpg_buf_len = 0;
  uint8_t * _jpg_buf = NULL;
  char * part_buf[64];

  res = httpd_resp_set_type(req, _STREAM_CONTENT_TYPE);
  if(res != ESP_OK){
    return res;
  }

  while(true){
    fb = esp_camera_fb_get();
    if (!fb) {
      res = ESP_FAIL;
    } else {
      if(fb->width > 400){
        if(fb->format != PIXFORMAT_JPEG){
          bool jpeg_converted = frame2jpg(fb, 80, &_jpg_buf, &_jpg_buf_len);
          esp_camera_fb_return(fb);
          fb = NULL;
          if(!jpeg_converted){
            res = ESP_FAIL;
          }
        } else {
          _jpg_buf_len = fb->len;
          _jpg_buf = fb->buf;
        }
      }
    }
    if(res == ESP_OK){
      size_t hlen = snprintf((char *)part_buf, 64, _STREAM_PART, _jpg_buf_len);
      res = httpd_resp_send_chunk(req, (const char *)part_buf, hlen);
    }
    if(res == ESP_OK){
      res = httpd_resp_send_chunk(req, (const char *)_jpg_buf, _jpg_buf_len);
    }
    if(res == ESP_OK){
      res = httpd_resp_send_chunk(req, _STREAM_BOUNDARY, strlen(_STREAM_BOUNDARY));
    }
    if(fb){
      esp_camera_fb_return(fb);
      fb = NULL;
      _jpg_buf = NULL;
    } else if(_jpg_buf){
      free(_jpg_buf);
      _jpg_buf = NULL;
    }
    if(res != ESP_OK){
      break;
    }
    //Serial.printf("MJPG: %uB\n",(uint32_t)(_jpg_buf_len));
  }
  return res;
}

static esp_err_t cmd_handler(httpd_req_t *req){
  char*  buf;
  size_t buf_len;
  char variable[200] = {0,};
  
  buf_len = httpd_req_get_url_query_len(req) + 1;
  if (buf_len > 1) {
    buf = (char*)malloc(buf_len);
    if(!buf){
      httpd_resp_send_500(req);
      return ESP_FAIL;
    }
    if (httpd_req_get_url_query_str(req, buf, buf_len) == ESP_OK) {
      if (httpd_query_key_value(buf, "go", variable, sizeof(variable)) == ESP_OK) {
      } else {
        free(buf);
        httpd_resp_send_404(req);
        return ESP_FAIL;
      }
    } else {
      free(buf);
      httpd_resp_send_404(req);
      return ESP_FAIL;
    }
    free(buf);
  } else {
    httpd_resp_send_404(req);
    return ESP_FAIL;
  }

  sensor_t * s = esp_camera_sensor_get();

  Serial.println(variable);
  
 

  httpd_resp_set_hdr(req, "Access-Control-Allow-Origin", "*");
  return httpd_resp_send(req, NULL, 0);
}

void startCameraServer(){
  httpd_config_t config = HTTPD_DEFAULT_CONFIG();
  config.server_port = 80;
  httpd_uri_t index_uri = {
    .uri       = "/",
    .method    = HTTP_GET,
    .handler   = index_handler,
    .user_ctx  = NULL
  };

  httpd_uri_t cmd_uri = {
    .uri       = "/action",
    .method    = HTTP_GET,
    .handler   = cmd_handler,
    .user_ctx  = NULL
  };
  httpd_uri_t stream_uri = {
    .uri       = "/stream",
    .method    = HTTP_GET,
    .handler   = stream_handler,
    .user_ctx  = NULL
  };
  if (httpd_start(&camera_httpd, &config) == ESP_OK) {
    httpd_register_uri_handler(camera_httpd, &index_uri);
    httpd_register_uri_handler(camera_httpd, &cmd_uri);
  }
  config.server_port += 1;
  config.ctrl_port += 1;
  if (httpd_start(&stream_httpd, &config) == ESP_OK) {
    httpd_register_uri_handler(stream_httpd, &stream_uri);
  }
}

void setup() {
  WRITE_PERI_REG(RTC_CNTL_BROWN_OUT_REG, 0); //disable brownout detector
  
 
  
  Serial.begin(115200);
  Serial.setDebugOutput(false);
  camera_config_t config;
  config.ledc_channel = LEDC_CHANNEL_0;
  config.ledc_timer = LEDC_TIMER_0;
  config.pin_d0 = Y2_GPIO_NUM;
  config.pin_d1 = Y3_GPIO_NUM;
  config.pin_d2 = Y4_GPIO_NUM;
  config.pin_d3 = Y5_GPIO_NUM;
  config.pin_d4 = Y6_GPIO_NUM;
  config.pin_d5 = Y7_GPIO_NUM;
  config.pin_d6 = Y8_GPIO_NUM;
  config.pin_d7 = Y9_GPIO_NUM;
  config.pin_xclk = XCLK_GPIO_NUM;
  config.pin_pclk = PCLK_GPIO_NUM;
  config.pin_vsync = VSYNC_GPIO_NUM;
  config.pin_href = HREF_GPIO_NUM;
  config.pin_sscb_sda = SIOD_GPIO_NUM;
  config.pin_sscb_scl = SIOC_GPIO_NUM;
  config.pin_pwdn = PWDN_GPIO_NUM;
  config.pin_reset = RESET_GPIO_NUM;
  config.xclk_freq_hz = 20000000;
  config.pixel_format = PIXFORMAT_JPEG; 
  
  if(psramFound()){
    config.frame_size = FRAMESIZE_VGA;
    config.jpeg_quality = 10;
    config.fb_count = 2;
  } else {
    config.frame_size = FRAMESIZE_SVGA;
    config.jpeg_quality = 12;
    config.fb_count = 1;
  }
  
  // Camera init
  esp_err_t err = esp_camera_init(&config);
  if (err != ESP_OK) {
    return;
  }
 
  // Wi-Fi connection
  WiFi.softAP(ssid, password);
  // Start streaming web server
  startCameraServer();
}

void loop() {
  
}
```

A primeira parte do c√≥digo diz respeito a configura√ß√£o inicial da c√¢mera, onde s√£o inclu√≠das as bibliotecas necess√°rias para o funcionamento do componente e tamb√©m algumas defini√ß√µes de portas. Aten√ß√£o: A depender do projeto, a defini√ß√£o das portas pode mudar, sendo que a defini√ß√£o atual √© para projetos CAMERA_MODEL_AI_THINKER.

````c
const char* ssid = "UoLi";
const char* password = "Carrinho";
````
Essa parte do c√≥digo se refere √†s credenciais do Wifi que ser√° gerado pelo Carrinho.
Logo em seguida √© montado o html da p√°gina que ser√° criada no IP da c√¢mera. (Como em nosso projeto a ESP-32 ir√° gerar o wifi, o IP ser√° sempre est√°tico em 192.168.4.1)
Note que na p√°gina, inclu√≠mos uma imagem (que √© a visualiza√ß√£o da c√¢mera) e uma s√©rie de bot√µes, que fazem requisi√ß√µes GET para o servidor da ESP32-CAM no caminho IP/action?go=
√â baseado nisso que fizemos as requisi√ß√µes tamb√©m no aplicativo, sendo que o mesmo segue a mesma l√≥gica da requisi√ß√£o GET.

````html
static const char PROGMEM INDEX_HTML[] = R"rawliteral(
<html>
  <head>
    <title>ESP32-CAM Robot</title>
    <meta name="viewport" content="width=device-width, initial-scale=1">
    <style>
      body { font-family: Arial; text-align: center; margin:0px auto; padding-top: 30px; background-color: #1b1b1b;}
      table { margin-left: auto; margin-right: auto; }
      td { padding: 8 px; }
      .button {
        background-color: #8969fb;
        border: none;
        color: white;
        padding: 10px 20px;
        text-align: center;
        text-decoration: none;
        display: inline-block;
        font-size: 18px;
        margin: 6px 3px;
        cursor: pointer;
        width: 100%;
        -webkit-touch-callout: none;
        -webkit-user-select: none;
        -khtml-user-select: none;
        -moz-user-select: none;
        -ms-user-select: none;
        user-select: none;
        -webkit-tap-highlight-color: rgba(0,0,0,0);
        
      }
      .button::placeholder{
          color: white;
      }
      img {  width: auto ;
        max-width: 100% ;
        height: auto ; 
      }
    </style>
  </head>
  <body>
    <img src="" id="photo" >
    
    <table>
      <tr><td colspan="3" align="center"><button class="button" ontouchstart="toggleCheckbox('forward');" ontouchend="toggleCheckbox('stop');" >Forward</button></td></tr>
      <tr><td align="center"><button class="button" ontouchstart="toggleCheckbox('left');" ontouchend="toggleCheckbox('stop');">Left</button></td><td align="center"><button class="button" ontouchstart="toggleCheckbox('stop');">Stop</button></td><td align="center"><button class="button" ontouchstart="toggleCheckbox('right');"  ontouchend="toggleCheckbox('stop');">Right</button></td></tr>
      <tr><td colspan="3" align="center"><button class="button" ontouchstart="toggleCheckbox('backward');" ontouchend="toggleCheckbox('stop');">Backward</button></td></tr>                   
    <tr><td colspan="2" align="center"><button class="button" ontouchstart="toggleCheckbox('l');">Light</button></td><td colspan="1" align="center"><button class="button" ontouchstart="toggleCheckbox('M'+document.getElementById('texto').value);">Send</button></td></tr>
    <tr><td colspan="3" align="center"><input id="texto" class="button" placeholder="Mensagem"></td></tr>
    </table>
    
   <script>
   function toggleCheckbox(x) {
     var xhr = new XMLHttpRequest();
     xhr.open("GET", "/action?go=" + x, true);
     xhr.send();
   }
   window.onload = document.getElementById("photo").src = window.location.href.slice(0, -1) + ":81/stream";
  </script>
  </body>
</html>
)rawliteral";
````
J√° a fun√ß√£o abaixo √© respons√°vel por receber as requisi√ß√µes de rede. Pode-se ajustar o tamanho da variable, que diz respeito ao tamanho m√°ximo da mensagem que pode ser recebeda. Nessa caso, se uma requisi√ß√£o tiver mais que 200 caracteres. 
Essas requisi√ß√µes ser√£o mensagens enviadas do usu√°rio para a c√¢mera, que podem fazer os mais diversos efeitos. No projeto, a c√¢mera ir√° receber, por exemplo, comandos de dire√ß√£o (forward, backward, left and right), mensagem para serem exibidas na tela do dispositivo e tamb√©m comando para ligar/desligar a lanterna.
Para enviar as mensagens por comunica√ß√£o serial, a c√¢mera faz simplesmente println da variable recebida, o que ser√° recebido pelo Mega, processado e executado. Os detalhes da conex√£o serial entre os arduinos ser√£o colocados mais para frente.

````c
static esp_err_t cmd_handler(httpd_req_t *req){
  char*  buf;
  size_t buf_len;
  char variable[200] = {0,};
  
  buf_len = httpd_req_get_url_query_len(req) + 1;
  if (buf_len > 1) {
    buf = (char*)malloc(buf_len);
    if(!buf){
      httpd_resp_send_500(req);
      return ESP_FAIL;
    }
    if (httpd_req_get_url_query_str(req, buf, buf_len) == ESP_OK) {
      if (httpd_query_key_value(buf, "go", variable, sizeof(variable)) == ESP_OK) {
      } else {
        free(buf);
        httpd_resp_send_404(req);
        return ESP_FAIL;
      }
    } else {
      free(buf);
      httpd_resp_send_404(req);
      return ESP_FAIL;
    }
    free(buf);
  } else {
    httpd_resp_send_404(req);
    return ESP_FAIL;
  }

  sensor_t * s = esp_camera_sensor_get();
  
  //ENVIANDO A REQUISI√á√ÉO RECEBIDA DIRETAMENTE PARA O ARDUINO MEGA VIA CONEX√ÉO SERIAL
  Serial.println(variable);
  
 

  httpd_resp_set_hdr(req, "Access-Control-Allow-Origin", "*");
  return httpd_resp_send(req, NULL, 0);
}
````

Por fim, dentro da fun√ß√£o setup, al√©m de ter mais algumas configura√ß√µes da c√¢mera, temos tamb√©m a inst√¢ncia da comunica√ß√£o serial e tamb√©m a inicializa√ß√£o do ponto de acesso Wifi, onde √© passado o ssid e password da rede, e tamb√©m o servidor da c√¢mera.

````c
// Wi-Fi connection
  WiFi.softAP(ssid, password);
  // Start streaming web server
  startCameraServer();
````

### Comunica√ß√£o Serial ESP32-CAM 
Como a c√¢mera √© independente, sua liga√ß√£o basicamente consiste em ligar os pinos GND e 5V. Todavia, para fazer a comunica√ß√£o serial entre a c√¢mera e o Arduino Mega, foi necess√°rio fazer uma liga√ß√£o dos Seriais da c√¢mera com os do arduino Mega. 
A imagem abaixo mostra os pinos da ESP32-CAM, onde pode-se observar que os pinos GPIO 1 e 3 s√£o, respectivamente, os pinos TX e RX. Assim, quando a c√¢mera faz comunica√ß√£o serial, como println(), os dados ser√£o transmitidos por essas portas. 

  ![image](https://lobodarobotica.com/blog/wp-content/uploads/2020/08/ESP32-CAM-pinout-new.png)
  
J√° o arduino Mega possui mais de um conjunto de portas seriais, o que torna poss√≠vel o projeto. Isso porque, no Mega, o primeiro conjunto de portas seriais estar√° sendo usado pela display LCD. Assim, n√£o bastaria um arduino Uno, por exemplo, pois este n√£o conseguiria controlar o display e receber mensagens da ESP32-CAM.

A montagem da circuito que possibilita essa comunica√ß√£o pode ser vista abaixo. Destaca-se que os pinos TX e RX sempre devem ficar invertidos entre si, ou seja, pino TX conectado com RX e o contr√°rio. 

  ![image](https://i.ibb.co/pwQXzxv/ESP-MEGA.png)
  
## Display LCD
Por √∫ltimo, e realmente menos importante, temos o Display LCD TFT 2.4 Touch Screen, que em nosso projeto tem a miss√£o apenas deixar o rob√¥ mais agrad√°vel e amig√°vel.

  ![image](https://http2.mlstatic.com/D_NQ_NP_717788-MLB31359661286_072019-O.webp)
  
O componente foi desenvolvido para ser encaixado diretamente na placa no ardu√≠no, por isso, sua liga√ß√£o √© facilitada. Veja abaixo como deve ser feita a liga√ß√£o. Recomenda-se fortemente pesquisas adicionais.

  ![image](https://i.ibb.co/19CVYmL/TFT-MEGA.png)
  
Ap√≥s o encaixe, √© poss√≠vel programar a display a partir do arduino. 
A seguinte refer√™ncia mostra como funciona toda a programa√ß√£o do m√≥dulo utilizando-se da biblioteca MCUFRIEND_kbv, que possibilita manipular diversos displays distintos, incluindo o modelo usado no projeto.
https://portal.vidadesilicio.com.br/primeiros-passos-shield-lcd-tft-24-touchscreen/

O funcionamento √© muito simples. Temos algumas fun√ß√µes que podem ser usadas, como drawFastHLine(), setTextColor, setTextSize, fillScreen(), etc.
Para come√ßar, precisa-se fazer o include da biblioteca e instanciar o objeto.
````c
#include <MCUFRIEND_kbv.h>
MCUFRIEND_kbv tft;
````
Depois, basta usar as fun√ß√µes do objeto tft. Abaixo um exemplo simples de uso:

````c
#include <MCUFRIEND_kbv.h>
MCUFRIEND_kbv tft;

void setup() {
  Serial.begin(115200);
  uint16_t ID = tft.readID();
  //assim pode-se descobrir qual o driver do display
  Serial.print("Identificador do display: ");
  Serial.println(ID, HEX);
  
  //iniciando serial do display
  tft.begin(ID);
  //girando a tela no sentido correto
  tft.setRotation(3);
  //pintando tudo de preto
  tft.fillScreen(TFT_BLACK);
  //trocando cor do texto para roxo e tamanho para 10 (isso √© bem grande para a tela)
  tft.setTextColor(ROXO);
  tft.setTextSize(10);
  //ajustando o cursor para escrever o texto centralizado
  tft.setCursor(20, 90);
  //escrevendo texto de boas vindas
  tft.print("UoLi!");
  delay(3000);
}
````
Vamos explicar agora algumas das decis√µes de projeto e tamb√©m explicar as fun√ß√µes criadas a partir disso.

### Divis√£o da tela
A resolu√ß√£o do display TFT √© de 320x240. Para facilitar o trabalho com a tela, simplificamos a propor√ß√£o da mesma por 16
![image](https://i.ibb.co/ng1zf05/Display-divis-o-1.png)
Assim, sempre trabalhamos com n√∫meros menores e facilitamos a l√≥gica para construir imagens. 
Fizemos uma fun√ß√£o para converter as propor√ß√µes. Como sempre cada divis√£o tem 16px, ent√£o basta simplesmente multiplicar a propor√ß√£o desejada por 16 e teremos o valor real da posi√ß√£o do pixel. 
```c
int getPosi(int prop){
  return 16*prop;
}
```

### Desenhando um ret√¢ngulo
A biblioteca utilizada n√£o tem muitas fun√ß√µes nativas, assim, fez-se necess√°rio criar algumas delas para facilitar o trabalho.
Existem duas fun√ß√µes criadas para desenhar um ret√¢ngulo na tela: retangulo() e retanguloDelay().
Basicamente as duas fazem a mesma fun√ß√£o, ambas recebem como par√¢metro a posi√ß√£o x e y que deseja-se come√ßar a desenhar a forma, a altura e largura que a mesma deve ter e a sua cor. A diferen√ßa √© que a fun√ß√£o retanguloDelay ir√° desenhar o ret√¢ngulo de modo que cada linha horizontal renderizada para criar a figura ir√° demorar x tempo de delay para desenhar a pr√≥xima, assim o ret√¢ngulo √© formado com uma esp√©cie de anima√ß√£o (a fun√ß√£o recebe como √∫ltimo par√¢metro o speed, que √© o valor do delay a ser aplicado entre as linhas).
```c
void retangulo(int x, int y, int altura, int largura, uint16_t cor){
  for(int i = y; i<y+altura; i++){
     tft.drawFastHLine(x, i, largura, cor);
  }
}

void retanguloDelay(int x, int y, int altura, int largura, uint16_t cor, int speed){
  for(int i = y; i<y+altura; i++){
     tft.drawFastHLine(x, i, largura, cor);
     delay(speed);
  }
}
```

### Desenhando os olhos
Para desenhar os olhos, criamos uma fun√ß√£o predeterminada para essa tarefa. Nela, basicamente √© colocado um ret√¢ngulo branco e no seu centro um outro ret√¢ngulo menor com a cor do olho do robo (definida no inicio do c√≥digo). Para a fun√ß√£o √© necess√°rio passar apenas a posi√ß√£o x do olho, assim, para criar os dois olhos √© necess√°rio chamar a fun√ß√£o duas vezes e indicar as posi√ß√µes de cada um.
````c
void olho(int x){
  for(int i = getPosi(2); i<getPosi(7); i++){
    tft.drawFastHLine(getPosi(x), i, 80, TFT_WHITE);
  }
  for(int i = getPosi(3)+10; i<getPosi(6)-10; i++){
    tft.drawFastHLine(getPosi(x)+25+olhoDir, i, 30, OLHO);
  }
  
}
````

### Construindo o rosto
Para fazer o rosto do robo, tamb√©m fizemos uma fun√ß√£o, onde a tela √© preenchida de preto (para limpar o conte√∫do) depois uma s√©rie de pequenos ret√¢ngulos s√£o colocados lado a lado, onde a posi√ß√£o do eixo y √© alterada para dar a impress√£o de um sorriso/linha curva para o desenho. Depois √© adicionado os dois olhos nas posi√ß√µes 2 e 13.
````c
void makeFace(){
  tft.fillScreen(TFT_BLACK);
  retangulo(getPosi(2), getPosi(10), getPosi(1), getPosi(2), TFT_WHITE);
  retangulo(getPosi(4), getPosi(10)+10, getPosi(1), getPosi(2), TFT_WHITE);
  retangulo(getPosi(6), getPosi(10)+20, getPosi(1), getPosi(2), TFT_WHITE);
  retangulo(getPosi(8), getPosi(10)+30, getPosi(1), getPosi(2), TFT_WHITE);
  retangulo(getPosi(10), getPosi(10)+30, getPosi(1), getPosi(2), TFT_WHITE);
  retangulo(getPosi(12), getPosi(10)+20, getPosi(1), getPosi(2), TFT_WHITE);
  retangulo(getPosi(14), getPosi(10)+10, getPosi(1), getPosi(2), TFT_WHITE);
  retangulo(getPosi(16), getPosi(10), getPosi(1), getPosi(2), TFT_WHITE);
  //320 x 240 
  olho(2);
  olho(13);
}
````

E essas s√£o as fun√ß√µes desenvolvidas para a parte gr√°fica do processamento do Arduino Mega. Resta agora mostrar o funcionamento completo do mesmo, que incluir controlar os motores, receber mensagens da c√¢mera e, √© claro, controlar as anima√ß√µes do display. 

## Arduino Mega
O Arduino Mega √© um dos principais agentes. Nele √© onde se concentra a maior parte do trabalho de programa√ß√£o desenvolvido.
Al√©m da programa√ß√£o que envolve a manibula√ß√£o do display, h√° ainda algumas outras tarefas, que s√£o:

1. Receber mensagens da ESP32-CAM via comunica√ß√£o Serial
2. Controlar o Driver ponte H
3. Controlar as anima√ß√µes do display

O c√≥digo completo carregado no Arduino Mega pode ser visto abaixo:
````c
/*
 *   C√ìDIGO:  Q0684
 *   AUTOR:   BrincandoComIdeias
 *   ACOMPANHE: https://www.youtube.com/brincandocomideias ; https://www.instagram.com/canalbrincandocomideias/
 *   APRENDA: https://cursodearduino.net/ ; https://cursoderobotica.net
 *   COMPRE:  https://www.arducore.com.br/
 *   SKETCH:  Verificar identificador  
 *   DATA:    15/09/2021
*/

// INCLUS√ÉO DE BIBLIOTECAS
#include <MCUFRIEND_kbv.h>

// DEFINI√á√ïES
#define ROXO   0xA602FB
#define FR 41 //forward right
#define BR 43 //backward right
#define FL 45 //forward left
#define BL 47 //backward left
#define LUZ A6
#define OLHO TFT_MAROON
// INSTANCIANDO OBJETOS
MCUFRIEND_kbv tft;

void setup() {
  //inicializando portas seriais
  Serial1.begin(115200);
  Serial.begin(115200);
  uint16_t ID = tft.readID();
  tft.begin(ID);
  
  //definindo pinMode
  pinMode(FR, OUTPUT);
  pinMode(BR, OUTPUT);
  pinMode(FL, OUTPUT);
  pinMode(BL, OUTPUT);
  pinMode(LUZ, OUTPUT);
  
  //parando os motores
  digitalWrite(FR, LOW);
  digitalWrite(BR, LOW);
  digitalWrite(FL, LOW);
  digitalWrite(BL, LOW);
  digitalWrite(LUZ, LOW);
  
  Serial.print("Identificador do display: ");
  Serial.println(ID, HEX);
  
  //escrevendo UoLi em texto grande na tela
  //tela de boas vindas
  tft.setRotation(3);
  tft.fillScreen(TFT_BLACK);
  tft.setTextColor(ROXO);
  tft.setTextSize(10);
  tft.setCursor(20, 90);
  tft.print("UoLi!");
  //espera um pouco na tela inicial e depois faz a rosto do robo
  delay(3000);
  makeFace();
  
}
//variaveis de controle

//quando estiver falando ele inicia a contagem para retirar a mensagem da tela
bool falando = false;
int cont_fala = 0;
//vari√°vel usada para fazer a contagem de tempo para piscar o olho do robo
int cont = 25;
//vari√°vel para o tamanho do texto das mensagens impressas na tela
int textSize = 2;
//vari√°vel que guarda o deslocamento do centro do olho no eixo x
//olhoDir>0 = olho deslocado para a direita, <0 para esquerda, 0 centro
int olhoDir = 0;
void loop() {
  //sempre que houver mensagens da ESP32, chama a fun√ß√£o de leitura
  if(Serial1.available()) serialComunication();
  
  //incrementa o contador para piscar 
  cont++;
  if(falando){
    //se estiver falando tamb√©m incrementa o contador para retirar a mensagem da tela
    cont_fala++;
    if(cont_fala==100){
      //se o contador chegou em 100 (ou seja, 100 loops, onde cada loop tem apenas um delay de 100ms, ou seja, ap√≥s 10000ms, ou 10s, a mensagem escrita √© apagada)
      //para apagar ele refaz o rosto do robo. Al√©m de zerar ambos os contadores e altarar falando para false
      makeFace();
      cont=0;
      cont_fala=0;
      falando = false;
    }
  }
  if(cont==50){
    //se o contador chegou em 50, ent√£o se passaram aproximadamente 5 segundo
    //para piscar o robo faz um ret√¢ngulo na √°rea do olho que ser√° preenchido com um pequeno delay
    //dando assim a impress√£o de fechar dos olhos da personagem.
    retanguloDelay(getPosi(2), getPosi(2), getPosi(4), getPosi(18), TFT_BLACK, 1);
    //espera um pouco com os olhos fechados e abre o olho, retorna o contador para 0;
    delay(10);
    olho(2);
    olho(13);
    cont=0;
  }
  delay(100);
}

void serialComunication(){
  
  while(Serial1.available()){
    char c = Serial1.read();
    switch(c){
      case 'F': 
        digitalWrite(FR, HIGH);
        digitalWrite(BR, LOW);
        digitalWrite(FL, HIGH);
        digitalWrite(BL, LOW);
        Serial.println("Frente");
        //muda dire√ß√£o do olho para centro (0 descolamento)
        olhoDir = 0;
        olho(2);
        olho(13);
        
      break;
      case 'B':
        digitalWrite(FR, LOW);
        digitalWrite(BR, HIGH);
        digitalWrite(FL, LOW);
        digitalWrite(BL, HIGH); 
        Serial.println("Tras");
        //muda dire√ß√£o do olho para centro (0 descolamento)
        olhoDir = 0;
        olho(2);
        olho(13);
      break;
      case 'R': 
        digitalWrite(FR, HIGH);
        digitalWrite(BR, LOW);
        digitalWrite(FL, LOW);
        digitalWrite(BL, HIGH);
        Serial.println("Direita");
        //muda dire√ß√£o do olho para direita do carrinho (esquerda da tela quando olhado de frente)
        olhoDir = -10;
        olho(2);
        olho(13);
      break;
      case 'L': 
        digitalWrite(FR, LOW);
        digitalWrite(BR, HIGH);
        digitalWrite(FL, HIGH);
        digitalWrite(BL, LOW);
        Serial.println("Esquerda");
        //muda dire√ß√£o do olho para esqueda do carrinho (direita da tela quando olhado de frente)
        olhoDir = 10;
        olho(2);
        olho(13);
      break;
      case 'S':
        digitalWrite(FR, LOW);
        digitalWrite(BR, LOW);
        digitalWrite(FL, LOW);
        digitalWrite(BL, LOW); 
        Serial.println("Para");
        //muda dire√ß√£o do olho para centro (0 descolamento)
        olhoDir = 0;
        olho(2);
        olho(13);
      break;
      case 'l':
        if(digitalRead(LUZ)==LOW){
          digitalWrite(LUZ, HIGH);
        }else{
          digitalWrite(LUZ, LOW);
        }
      break;
      case 'm': 
          
          String mensagem = "";
          Serial.println("Mensagem:");
          c = Serial1.read();
          if(c=='L'){
            if(Serial1.available()){
              c = Serial1.read();
              if(c=='+'){
                textSize++;
                mensagem="Assim ta bom?";
              }else if(c=='-'){
                textSize--;
                mensagem="Assim ta bom?";
              }else{
                mensagem= "L";
                mensagem.concat(c);
              }
            }else
                mensagem.concat(c);              
          }else
          if(c == '%'){
            if(Serial1.available()>0){
              c = Serial1.read();
              if(c=='2'){                  
                c = Serial1.read();
                mensagem.concat(' ');
              }else{
                mensagem.concat('%');
                mensagem.concat(c);
              }
            }else{
              mensagem.concat('%');
            }
          }else{
            mensagem.concat(c);
          }
          while(Serial1.available()>0){
            c = Serial1.read();
            if(c == '%'){
              if(Serial1.available()>0){
                c = Serial1.read();
                if(c=='2'){                  
                  c = Serial1.read();
                  mensagem.concat(' ');
                }else{
                  mensagem.concat('%');
                  mensagem.concat(c);
                  
                }
              }
            }else 
            if (c != '\n'){
              // Concatena valores
              mensagem.concat(c);
            }
             delay(10);
          }
          
          tft.setTextColor(TFT_BLACK);
          tft.setTextSize(textSize);
          tft.setCursor(30, getPosi(8)+10);
          retanguloDelay(getPosi(0), getPosi(8), getPosi(5), getPosi(20), TFT_WHITE, 2);
          tft.print(mensagem);
          cont_fala=0;
          falando=true;
          Serial.println(mensagem);
      break;
      default:
          tft.setTextColor(TFT_BLACK);
          tft.setTextSize(4);
          tft.setCursor(30, getPosi(8)+10);
          retanguloDelay(getPosi(0), getPosi(8), getPosi(5), getPosi(20), TFT_WHITE, 2);
          tft.print("N√£o entendi... ):");
          cont_fala=50;
          falando=true;
          Serial.println(mensagem);
      
  }
    
//      default:

    }
}

void makeFace(){
  tft.fillScreen(TFT_BLACK);
  retangulo(getPosi(2), getPosi(10), getPosi(1), getPosi(2), TFT_WHITE);
  retangulo(getPosi(4), getPosi(10)+10, getPosi(1), getPosi(2), TFT_WHITE);
  retangulo(getPosi(6), getPosi(10)+20, getPosi(1), getPosi(2), TFT_WHITE);
  retangulo(getPosi(8), getPosi(10)+30, getPosi(1), getPosi(2), TFT_WHITE);
  retangulo(getPosi(10), getPosi(10)+30, getPosi(1), getPosi(2), TFT_WHITE);
  retangulo(getPosi(12), getPosi(10)+20, getPosi(1), getPosi(2), TFT_WHITE);
  retangulo(getPosi(14), getPosi(10)+10, getPosi(1), getPosi(2), TFT_WHITE);
  retangulo(getPosi(16), getPosi(10), getPosi(1), getPosi(2), TFT_WHITE);
  //320 x 240 
  olho(2);
  olho(13);
}
int getPosi(int prop){
  return 16*prop;
}


void olho(int x){
  for(int i = getPosi(2); i<getPosi(7); i++){
    tft.drawFastHLine(getPosi(x), i, 80, TFT_WHITE);
  }
  for(int i = getPosi(3)+10; i<getPosi(6)-10; i++){
    tft.drawFastHLine(getPosi(x)+25+olhoDir, i, 30, OLHO);
  }
  
}

void retangulo(int x, int y, int altura, int largura, uint16_t cor){
  for(int i = y; i<y+altura; i++){
     tft.drawFastHLine(x, i, largura, cor);
  }
}

void retanguloDelay(int x, int y, int altura, int largura, uint16_t cor, int speed){
  for(int i = y; i<y+altura; i++){
     tft.drawFastHLine(x, i, largura, cor);
     delay(speed);
  }
}
````

### Comunica√ß√£o Serial Arduino Mega

A comunica√ß√£o serial no Arduino Mega acontece em dois conjuntos de portas. TX0 e RX0 e TX1 e RX1. O conjunto Serial (TX0 e RX0) √© utilizado pelo display LCD e o Serial1 √© utilizado pela ESP32-CAM.
Pela Serial, pode-se escrever textos no display, para isso basta inicializar (begin) o objeto tft e configurar a cor do texto, a posi√ß√£o do ponteiro, o tamanho da fonte e, √© claro, fazer o print com a informa√ß√£o a ser impressa. (Veja a se√ß√£o do display para entender melhor seu funcionamento)

J√° para a Serial1, a mesma ser√° respons√°vel por receber as mensagens enviadas pela ESP32-CAM. Essas mensagens s√£o capturadas pela fun√ß√£o serialComunication().
Essa fun√ß√£o primeiramente l√™ o primeiro caracter recebido, pois este determina a natureza da mensagem recebida.
Caso seja F, B, R, L ou S (mai√∫sculos), ent√£o a mensagem que chegou nada mais √© que um dos comando para controlar os motores.
Caso seja l (L min√∫sculo) esse √© um comando para ligar/desligar a lanterna do robo.
Por fim, se for m (tamb√©m min√∫sculo), ent√£o esta √© uma mensagem e devem haver mais caracteres a serem lidos a frente. Nesse caso, deve-se precupar com alguns casos, pois se a mensagem enviada por igual √† L+ ou L- ent√£o este tamb√©m √© um comando para aumentar ou diminuir o tamanho do texto. 
Al√©m disso, caso a mensagem contenha espa√ßos " ", ent√£o √© necess√°rio fazer um tratamento, pois, como as requisi√ß√µes s√£o mandados por rede para a ESP32-CAM, ent√£o espa√ßos se tornam "%20" dentro da URL enviada. Assim sempre que for lido o caracter %, deve-se verificar se ele est√° acompanhado de 20, pois se sim, este deve ser substituido por um espa√ßo.

### Controlando os motores
Para controlar os motores pelo arduino, usando o Driver ponte H, √© muito simples. Este √© feito na mesma fun√ß√£o de leitura das mensagens, serialComunication(). 
Antes, no setup(), √© necess√°rio:
1. Definir os pinos que controlar√£o de cada uma das entradas do Driver (no projeto s√£o usados os pinos 41, 43, 45 e 47)
2. Colocar o modo dos pinos em OUTPUT
3. Depois basta fazer as escritas nas portas com uma combina√ß√£o de acordo com a dire√ß√£o desejada.

Segue abaixo o c√≥digo de exemplo dessas a√ß√µes necess√°rias para controlar os motores.
```c
#define FR 41 //forward right
#define BR 43 //backward right
#define FL 45 //forward left
#define BL 47 //backward left
void setup() {
//[...]
  pinMode(FR, OUTPUT);
  pinMode(BR, OUTPUT);
  pinMode(FL, OUTPUT);
  pinMode(BL, OUTPUT);
  pinMode(LUZ, OUTPUT);
  
  //Para o carrinho n√£o come√ßar andando, abaixa-se todas as portas
  digitalWrite(FR, LOW);
  digitalWrite(BR, LOW);
  digitalWrite(FL, LOW);
  digitalWrite(BL, LOW);
  digitalWrite(LUZ, LOW);
  char c = 'F';
//[...]
}
//[...]
void serialComunication(){
//[...]
  switch(c){
      case 'F': 
        digitalWrite(FR, HIGH);
        digitalWrite(BR, LOW);
        digitalWrite(FL, HIGH);
        digitalWrite(BL, LOW);
        Serial.println("Frente");
        //muda dire√ß√£o do olho para centro (0 descolamento)
        olhoDir = 0;
        olho(2);
        olho(13);
        
      break;
      case 'B':
        digitalWrite(FR, LOW);
        digitalWrite(BR, HIGH);
        digitalWrite(FL, LOW);
        digitalWrite(BL, HIGH); 
        Serial.println("Tras");
        //muda dire√ß√£o do olho para centro (0 descolamento)
        olhoDir = 0;
        olho(2);
        olho(13);
      break;
      case 'R': 
        digitalWrite(FR, HIGH);
        digitalWrite(BR, LOW);
        digitalWrite(FL, LOW);
        digitalWrite(BL, HIGH);
        Serial.println("Direita");
        //muda dire√ß√£o do olho para direita do carrinho (esquerda da tela quando olhado de frente)
        olhoDir = -10;
        olho(2);
        olho(13);
      break;
      case 'L': 
        digitalWrite(FR, LOW);
        digitalWrite(BR, HIGH);
        digitalWrite(FL, HIGH);
        digitalWrite(BL, LOW);
        Serial.println("Esquerda");
        //muda dire√ß√£o do olho para esqueda do carrinho (direita da tela quando olhado de frente)
        olhoDir = 10;
        olho(2);
        olho(13);
      break;
      case 'S':
        digitalWrite(FR, LOW);
        digitalWrite(BR, LOW);
        digitalWrite(FL, LOW);
        digitalWrite(BL, LOW); 
        Serial.println("Para");
        //muda dire√ß√£o do olho para centro (0 descolamento)
        olhoDir = 0;
        olho(2);
        olho(13);
      break;
      //[..]
}
```

### Anima√ß√µes da tela/rosto rob√¥
O rob√¥ possui algumas anima√ß√µes envolvendo seu display, essas s√£o:
1. Piscar os olhos
2. "Falar"
3. Aumentar tamanho do texto da "fala"
4. Olhar para os lados 

## Esquema completo liga√ß√µes
O esquema abaixo mostra todas as conex√µes entre os componentes dentro do carrinho UoLi, bem como uma lista das conex√µes.
![image](https://i.ibb.co/GPPfCv1/PROJETO-COMPLETO-1.png)

